#version 450
#extension GL_KHR_shader_subgroup_shuffle: enable
#define WORKGROUP_SIZE ~WG_SIZE~

layout(local_size_x = WORKGROUP_SIZE, local_size_y = 1) in;

layout(set = 0, binding = 0) buffer BM {
    uint[32] bms[];
};
struct Uniforms
{
    uint num_bms;
    uint num_executions;
};
layout(set=0, binding = 1) uniform UniformInput
{
    Uniforms u_consts;
};

uint shuffle_round(uint dst_tix, uint a, uint m, uint s) {
    uint b = subgroupShuffleXor(a, s);
    uint c;
    // this predicate is unlike the pure-SIMD shuffle function, which uses gl_SubgroupInvocationID
    if ((dst_tix & s) == 0) {
        c = b << s;
    } else {
        m = ~m;
        c = b >> s;
    }
    return (a & m) | (c & ~m);
}

// Assuming 32x32 bit matrices, we expect at most gl_WorkGroupSize.x >> 5 to be processed by the workgroup.
const uint num_mats_per_wg = gl_WorkGroupSize.x >> 5;
shared uint[32] tg_bms[num_mats_per_wg];

void main() {
    uint local_bm_ix = gl_LocalInvocationID.x >> 5;
    uint global_bm_ix = num_mats_per_wg*gl_WorkGroupID.x + local_bm_ix;

    if (global_bm_ix < u_consts.num_bms && local_bm_ix < u_consts.num_bms) {
        uint dst_tix = gl_LocalInvocationID.x;
        uint dst_rix = dst_tix & 31;
        uint global_row = bms[global_bm_ix][dst_rix];
        uint row;

        for (uint iter = 0; iter < u_consts.num_executions; iter++) {
            row = global_row;
            row = shuffle_round(dst_tix, row, 0xf0f0f0f, 4);
            row = shuffle_round(dst_tix, row, 0x33333333, 2);
            row = shuffle_round(dst_tix, row, 0x55555555, 1);
        }

        bms[global_bm_ix][dst_rix] = row;
    }
}
